stylesheet "1.0";
include "../utils/strings.xsl";
include "../utils/token-strings.xsl";
include "../utils/die.xsl";
output method="xml" indent="yes";
output omit-xml-declaration="no";

//////////////////////////////////////////////////////////////////////
// Stylesheet parameters
//////////////////////////////////////////////////////////////////////

// The name of the article .def that we will create
#article = { ""; }

// The directory in which we will look for the dco file
#prel-directory = { ""; }

// Name of a dco file (sans extension) in which we shall look for
// other functions and predicates.
#reference-dco = { ""; }

#original = { ""; }

#background-article-name = { ""; }

#background-tptp-xml = { ""; }

//////////////////////////////////////////////////////////////////////
// Keys
//////////////////////////////////////////////////////////////////////

key "formulas" [formula[@name]] `@name`;

//////////////////////////////////////////////////////////////////////
// Templates
//////////////////////////////////////////////////////////////////////

tpl [/] {
  choose {
    when [$article = ""] {
      apply [.] (#message = "Error: a value for the article stylesheet parameter was not supplied (or the empty string was supplied).") mode="die";
    }
    when [not(tstp)] {
      apply [.] (#message = "Error: this does not appear to be a TSTP XML document, because it lacks a 'tstp' root element.") mode="die";
    }
    when [tstp[2]] {
      apply [.] (#message = "Error: this does not appear to be a TSTP XML document, because it has multiple 'tstp' root elements.") mode="die";
    }
    otherwise {
      apply [tstp];
    }
  }
}

tpl [tstp] {
  $article-uc = uc (#s = `$article`);
  $reference-dco-uc = uc (#s = `$reference-dco`);
  <Theorems {
    <Signature {
      <ArticleID {
        @"name" = "HIDDEN";
      }
      if [not($reference-dco = "")] {
        <ArticleID {
          @"name" = $reference-dco-uc;
        }
      }
      <ArticleID {
        @"name" = $article-uc;
      }
    }

    // Generate the axioms
    for-each [formula[@status = "axiom"]] {
      <Theorem {
        @kind = "T";
        apply [.] (#variable-alist = ",,") mode="render-definiens";
      }
    }

  }
}

tpl [*] (#variable-alist) mode="render-definiens" {
  $n = `name (.)`;
  $message = `concat ("Error: we have arrived at an unhandled ", $n, " node in render-definiens mode.")`;
  apply [.] (#message = $message) mode="die";
}

tpl [formula] (#variable-alist) mode="render-definiens" {
  for-each [*[1]] { // the formula proper of the TPTP formula
    apply [.] (#variable-alist = $variable-alist) mode="render-definiens";
  }
}

tpl [quantifier[@type = "universal"]] (#started, #variables, #variable-number, #variable-alist) mode="render-definiens" {
  if [$started = "1"] {
    if [$variables] {
      $variable = `$variables[1]`;
      $variable-name = `$variable/@name`;
      $item = `concat ($variable-name, ":", $variable-number)`;
      $new-variable-alist = add-to-token-string (#item = $item, #token-string = $variable-alist);
      <For {
        <Typ {
          @kind = "M";
          @nr = "1";
          <Cluster;
        }
        apply [.] (#started = "1",
                   #variables = `$variables[position() > 1]`,
                   #variable-number = `$variable-number + 1`,
                   #variable-alist = $new-variable-alist) mode="render-definiens";

      }
    } else {
      apply [*[position() = last()]] (#variable-alist = $variable-alist) mode="render-definiens";
    }
  } else {
    $num-variables-bound-so-far
      = count-items-in-token-string (#token-string = $variable-alist);
    apply [.] (#started = "1",
               #variable-number = `$num-variables-bound-so-far + 1`,
               #variables = `variable`,
               #variable-alist = $variable-alist) mode="render-definiens";
  }
}

tpl [quantifier[@type = "existential"]] (#started, #variables, #variable-number, #variable-alist) mode="render-definiens" {
  if [$started = "1"] {
    if [$variables] {
      $variable = `$variables[1]`;
      $variable-name = `$variable/@name`;
      $item = `concat ($variable-name, ":", $variable-number)`;
      $new-variable-alist = add-to-token-string (#item = $item, #token-string = $variable-alist);
      <Not {
        <For {
          <Typ {
            @kind = "M";
            @nr = "1";
            <Cluster;
          }
          <Not {
            apply [.] (#started = "1",
                       #variables = `$variables[position() > 1]`,
                       #variable-number = `$variable-number + 1`,
                       #variable-alist = $new-variable-alist) mode="render-definiens";

          }
        }
      }
    } else {
      apply [*[position() = last()]] (#variable-alist = $variable-alist) mode="render-definiens";
    }
  } else {
    $num-variables-bound-so-far
      = count-items-in-token-string (#token-string = $variable-alist);
    apply [.] (#started = "1",
               #variable-number = `$num-variables-bound-so-far + 1`,
               #variables = `variable`,
               #variable-alist = $variable-alist) mode="render-definiens";
  }
}

tpl [conjunction] (#variable-alist) mode="render-definiens" {
  <And {
    apply [*] (#variable-alist = $variable-alist) mode="render-definiens";
  }
}

tpl [disjunction] (#variable-alist) mode="render-definiens" {
  <Not {
    <And {
      <Not {
        apply [*[1]] (#variable-alist = $variable-alist) mode="render-definiens";
      }
      <Not {
        apply [*[2]] (#variable-alist = $variable-alist) mode="render-definiens";
      }
    }
  }
}

tpl [negation] (#variable-alist) mode="render-definiens" {
  <Not {
    apply [*] (#variable-alist = $variable-alist) mode="render-definiens";
  }
}

tpl [implication] (#variable-alist) mode="render-definiens" {
  <Not {
    <And {
      apply [*[1]] (#variable-alist = $variable-alist) mode="render-definiens";
      <Not {
        apply [*[2]] (#variable-alist = $variable-alist) mode="render-definiens";
      }
    }
  }
}

tpl [equivalence] (#variable-alist) mode="render-definiens" {
  <And {
    <Not {
      <And {
        apply [*[1]] (#variable-alist = $variable-alist) mode="render-definiens";
        <Not {
          apply [*[2]] (#variable-alist = $variable-alist) mode="render-definiens";
        }
      }
    }
    <Not {
      <And {
        apply [*[2]] (#variable-alist = $variable-alist) mode="render-definiens";
        <Not {
          apply [*[1]] (#variable-alist = $variable-alist) mode="render-definiens";
        }
      }
    }
  }
}

tpl [predicate[@name = "="]] (#variable-alist) mode="render-definiens" {
  <Pred {
    @kind = "R";
    @nr = "1";  // magic constant: equality is the very first relation in HIDDEN
    apply [*] (#variable-alist = $variable-alist) mode="render-definiens";
  }
}

tpl count-predicates {
  `count (descendant::predicate[not(@name = "=")
                                and not(@name = preceding::predicate/@name)])`;
}

tpl [predicate[not(@name = "=")]] (#variable-alist) mode="render-definiens" {
  $n = `@name`;
  <Pred {
    @kind = "R";
    @spelling = `@name`;
    if [$background-article-name = ""] {
      for-each [ancestor::tstp] {
        for-each [descendant::predicate[@name = $n][1]] {
          $position = `count (preceding::predicate[not(@name = "=")
                                                   and not(@name = preceding::predicate/@name)]) + 1`;
          @nr = `$position + 2`;
        }
      }
    } else {
      for-each [document ($background-tptp-xml, /tstp)] {
        if [descendant::predicate[@name = $n][1]] {
          for-each [descendant::predicate[@name = $n][1]] {
            $position = `count (preceding::predicate[not(@name = "=")
                                                     and not(@name = preceding::predicate/@name)]) + 1`;
            @nr = `$position + 2`;
          }
        } else {
          $message = `concat ("Error: cannot find predicate '", $n, "' in ", $background-tptp-xml, ".")`;
          apply [.] (#message = $message) mode="die";
        }
      }
    }
    apply [*] (#variable-alist = $variable-alist) mode="render-definiens";
  }
}

tpl [variable[@name]] (#variable-alist) mode="render-definiens" {
  $n = `@name`;
  $pseudo-token = `concat (",", $n, ":")`;
  if [contains ($variable-alist, $pseudo-token)] {
    $after-pseudo-token = `substring-after ($variable-alist, $pseudo-token)`;
    $variable-number = `substring-before ($after-pseudo-token, ",")`;
    <Var {
      @nr = $variable-number;
    }
  } else {
    $message = `concat ("Error: the variable '", $n, "' does not appear in the variable association list '", $variable-alist, "'.")`;
    apply [.] (#message = $message) mode="die";
  }
}

tpl position-of-new-function-1 (#function-name, #candidates) {
  if [$candidates] {
    $candidate = `$candidates[1]`;
    $candidate-name = `$candidate/@name`;
    for-each [document ($background-tptp-xml, /tstp)] {
      if [descendant::function[@name = $candidate-name]] {
        position-of-new-function-1 (#function-name = $function-name,
				    #candidates = `$candidates[position() > 1]`);
      } else {
        $pos-previous = position-of-new-function-1 (#function-name = $function-name,
				                    #candidates = `$candidates[position() > 1]`);
        `$pos-previous + 1`;
      }
    }
  } else {
    "1";
  }
}

tpl position-of-new-function (#function-name) {
  position-of-new-function-1 (#function-name = $function-name,
                              #candidates = `preceding::function[not(@name = preceding::function/@name) and not(@name = ancestor::function/@name)]`);
}

tpl count-functions {
  `count(descendant::function[not(@name = preceding::function/@name)
                              and not(@name = ancestor::function/@name)])`;
}

tpl [function[@name]] (#variable-alist) mode="render-definiens" {
  $n = `@name`;
  $function = `.`;
  <Func {
    @kind = "K";
    @spelling = `@name`;
    if [$background-article-name = ""] {
      for-each [ancestor::tstp] {
        for-each [descendant::function[@name = $n][1]] {
          $position
            = `count (preceding::function[not(@name = preceding::function/@name)
                                          and not(@name = ancestor::function/@name)]
                      | ancestor::function[not(@name = preceding::function/@name)
                                           and not(@name = ancestor::function/@name)])
               + 1`;
          @nr = $position;
        }
      }
    } else {
      for-each [document ($background-tptp-xml, /tstp)] {
        if [descendant::function[@name = $n][1]] {
          for-each [descendant::function[@name = $n][1]] {
            $position
              = `count (preceding::function[not(@name = preceding::function/@name)
                                            and not(@name = ancestor::function/@name)]
                        | ancestor::function[not(@name = preceding::function/@name)
                                             and not(@name = ancestor::function/@name)]) + 1`;
            @nr = `$position`;
          }
        } else {
          $num-old-functions = count-functions ();
          for-each [$function] {
            for-each [ancestor::tstp] {
              for-each [descendant::function[@name = $n][1]] {
                $nr = position-of-new-function (#function-name = $n);
                msg `concat ("position of new function ", $n, " is ", $nr)`;
                @nr = `$num-old-functions + $nr`;
              }
            }
          }
        }
      }
    }
    apply [*] (#variable-alist = $variable-alist) mode="render-definiens";
  }
}
