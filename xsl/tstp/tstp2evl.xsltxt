stylesheet "1.0";
include "../utils/strings.xsl";
include "../utils/token-strings.xsl";
output method="xml" indent="yes";

//////////////////////////////////////////////////////////////////////
// Stylesheet parameters
//////////////////////////////////////////////////////////////////////

#article = { ""; }

#background = { ""; }

//////////////////////////////////////////////////////////////////////
// Templates
//////////////////////////////////////////////////////////////////////

// If we don't handle something explicitly, we don't handle it at all.
tpl [*] {
  $n = `name (.)`;
  $message = `concat ("Error: we have arrived at an unhandled ", $n, " node.")`;
  msg terminate="yes" $message;
}

tpl [/] {
  choose {
    when [$article = ""] {
      msg terminate="yes" "Error: the empty string is not an acceptable value for the article stylesheet parameter.";
    }
    when [string-length ($article) > 8] {
      $too-long-message = `concat ("Error: the value (", $article, ") of the article stylesheet parameter is too long (it must be at most 8 characters).")`;
      msg terminate="yes" $too-long-message;
    }
    when [contains ($article, " ")] {
      msg terminate="yes" "Error: the value of the article stylesheet parameter contains whitespace.";
    }
    when [tstp] {
      apply [tstp] (#proofs = ",,");
    }
    otherwise {
      msg terminate="yes" "Error: the required tstp document element is missing.";
    }
  }
}

tpl ident (#s) {
  <Ident {
    @"name" = $s;
  }
}

tpl [tstp] {
  $article-uc = uc (#s = $article);
  $article-with-suffix = `concat ($article-uc, "_E")`;
  $background-uc = uc (#s = $background);
  $background-with-suffix = `concat ($background-uc, "_E")`;
  <Environ {
    @aid = $article-uc;
    <Directive {
      @"name" = "Vocabularies";
      if [not($background = "")] {
        ident (#s = $background-uc);
      }
      ident (#s = $article-uc);
    }
    <Directive {
      @"name" = "Notations";
      if [not($background = "")] {
        ident (#s = $background-with-suffix);
      }
      ident (#s = $article-with-suffix);
    }
    <Directive {
      @"name" = "Constructors";
      if [not($background = "")] {
        ident (#s = $background-with-suffix);
      }
      ident (#s = $article-with-suffix);
    }
    <Directive {
      @"name" = "Theorems";
      // ident (#s = $background-with-suffix);
      ident (#s = $article-with-suffix);
    }
  }
}
