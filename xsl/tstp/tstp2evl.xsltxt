stylesheet "1.0";
include "../utils/strings.xsl";
include "../utils/token-strings.xsl";
output method="xml" indent="yes";

//////////////////////////////////////////////////////////////////////
// Stylesheet parameters
//////////////////////////////////////////////////////////////////////

#article = { ""; }

#background-article-name = { ""; }

#background-tptp-xml = { ""; }

//////////////////////////////////////////////////////////////////////
// Templates
//////////////////////////////////////////////////////////////////////

// If we don't handle something explicitly, we don't handle it at all.
tpl [*] {
  $n = `name (.)`;
  $message = `concat ("Error: we have arrived at an unhandled ", $n, " node.")`;
  msg terminate="yes" $message;
}

tpl [/] {
  choose {
    when [$article = ""] {
      msg terminate="yes" "Error: the empty string is not an acceptable value for the article stylesheet parameter.";
    }
    when [string-length ($article) > 8] {
      $too-long-message = `concat ("Error: the value (", $article, ") of the article stylesheet parameter is too long (it must be at most 8 characters).")`;
      msg terminate="yes" $too-long-message;
    }
    when [contains ($article, " ")] {
      msg terminate="yes" "Error: the value of the article stylesheet parameter contains whitespace.";
    }
    when [tstp] {
      apply [tstp] (#proofs = ",,");
    }
    otherwise {
      msg terminate="yes" "Error: the required tstp document element is missing.";
    }
  }
}

tpl ident (#s) {
  <Ident {
    @"name" = $s;
  }
}

tpl maybe-emit-background-ident {
  if [not($background-article-name = "")] {
    $background-uc = uc (#s = $background-article-name);
    $background-with-suffix = `concat ($background-uc, "E")`;
    ident (#s = $background-with-suffix);
  }
}

tpl any-new-function-1 (#candidates) {
  if [$candidates] {
    $candidate = `$candidates[1]`;
    $candidate-name = `$candidate/@name`;
    for-each [document ($background-tptp-xml, /tstp)] {
      if [not(descendant::function[@name = $candidate-name])] {
        "1";
      } else {
        any-new-function-1 (#candidates = `$candidates[position() > 1]`);
      }
    }
  } else {
    "0";
  }
}

tpl any-new-function {
  any-new-function-1 (#candidates = `descendant::function[not(@name = preceding::function/@name) and not(@name = ancestor::function/@name)]`);
}

tpl any-new-predicate-1 (#candidates) {
  if [$candidates] {
    $candidate = `$candidates[1]`;
    $candidate-name = `$candidate/@name`;
    for-each [document ($background-tptp-xml, /tstp)] {
      if [not(descendant::predicate[@name = $candidate-name])] {
        "1";
      } else {
        any-new-predicate-1 (#candidates = `$candidates[position() > 1]`);
      }
    }
  } else {
    "0";
  }
}

tpl any-new-predicate {
  any-new-predicate-1 (#candidates = `descendant::predicate[not(@name = "=")
                                                            and not(@name = preceding::predicate/@name)]`);
}

tpl any-new-constructor {
  $any-new-function = any-new-function ();
  if [$any-new-function = "1"] {
    msg "new function";
    "1";
  } else {
    any-new-predicate ();
  }
}

tpl [tstp] {
  $article-uc = uc (#s = $article);
  $article-with-suffix = `concat ($article-uc, "E")`;
  <Environ {
    @aid = $article-uc;
    <Directive {
      @"name" = "Vocabularies";
      if [not($background-article-name = "")] {
        $background-uc = uc (#s = $background-article-name);
        ident (#s = $background-uc);
      }
      ident (#s = $article-uc);
    }
    <Directive {
      @"name" = "Notations";
      maybe-emit-background-ident ();
      // Is there any new notation at all?
      if [$background-article-name = ""] {
        ident (#s = $article-with-suffix);
      } else {
        msg "looking for new stuff";
        $anything = any-new-constructor ();
        if [$anything = "1"] {
          msg "new constructor";
          ident (#s = $article-with-suffix);
        } else {
          msg "no new notations";
        }
      }
    }
    <Directive {
      @"name" = "Constructors";
      maybe-emit-background-ident ();
      // Is there any new notation at all?
      if [$background-article-name = ""] {
        ident (#s = $article-with-suffix);
      } else {
        msg "looking for new stuff";
        $anything = any-new-constructor ();
        if [$anything = "1"] {
          msg "new constructor";
          ident (#s = $article-with-suffix);
        } else {
          msg "no new notations";
        }
      }
    }
    <Directive {
      @"name" = "Theorems";
      ident (#s = $article-with-suffix);
    }
  }
}
