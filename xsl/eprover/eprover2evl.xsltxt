stylesheet "1.0";
include "../utils/strings.xsl";
include "../utils/token-strings.xsl";
output method="xml" indent="yes";

//////////////////////////////////////////////////////////////////////
// Stylesheet parameters
//////////////////////////////////////////////////////////////////////

#article = { ""; }

// A token list of paths to XMLized TSTP proofs that we may need to
// insert
#external-proofs = { ""; }

//////////////////////////////////////////////////////////////////////
// Templates
//////////////////////////////////////////////////////////////////////

// If we don't handle something explicitly, we don't handle it at all.
tpl [*] {
  $n = `name (.)`;
  $message = `concat ("Error: we have arrived at an unhandled ", $n, " node.")`;
  msg terminate="yes" $message;
}

tpl [/] {
  choose {
    when [$article = ""] {
      msg terminate="yes" "Error: the empty string is not an acceptable value for the article stylesheet parameter.";
    }
    when [string-length ($article) > 8] {
      $too-long-message = `concat ("Error: the value (", $article, ") of the article stylesheet parameter is too long (it must be at most 8 characters).")`;
      msg terminate="yes" $too-long-message;
    }
    when [contains ($article, " ")] {
      msg terminate="yes" "Error: the value of the article stylesheet parameter contains whitespace.";
    }
    when [tstp] {
      if [$external-proofs = ""] {
        apply [tstp] (#proofs = ",,");
      } else {
        $valid-proofs = is-valid-token-string (#string = $external-proofs);
        if [$valid-proofs = "1"] {
          apply [tstp] (#proofs = $external-proofs);
        } else {
          $message = `concat ("Error: the value '", $external-proofs, "' supplied for the external-proofs stylesheet parameter is not a valid token string.")`;
          msg terminate="yes" $message;
        }
      }
    }
    otherwise {
      msg terminate="yes" "Error: the required tstp document element is missing.";
    }
  }
}

tpl [tstp] (#proofs) {
  $article-uc = uc (#s = $article);
  <Environ {
    @aid = $article-uc;
    <Directive {
      @"name" = "Vocabularies";
      <Ident {
        @"name" = $article-uc;
      }
      emit-idents-for-external-proofs (#proofs = $proofs);
    }
    <Directive {
      @"name" = "Notations";
      <Ident {
        @"name" = $article-uc;
      }
      emit-idents-for-external-proofs (#proofs = $proofs);
    }
    <Directive {
      @"name" = "Constructors";
      <Ident {
        @"name" = $article-uc;
      }
      emit-idents-for-external-proofs (#proofs = $proofs);
    }
    <Directive {
      @"name" = "Theorems";
      <Ident {
        @"name" = $article-uc;
      }
      emit-idents-for-external-proofs (#proofs = $proofs);
    }
  }
}

tpl emit-idents-for-external-proofs (#proofs) {
  $empty = token-string-is-empty (#token-string = $proofs);
  if [$empty = "1"] {
    // done
  } else {
    $problem = token-string-head (#token-string = $proofs);
    $tail = token-string-tail (#token-string = $proofs);
    <Ident {
      $problem-uc = uc (#s = $problem);
      @"name" = $problem-uc;
    }
    emit-idents-for-external-proofs (#proofs = $tail);
  }
}