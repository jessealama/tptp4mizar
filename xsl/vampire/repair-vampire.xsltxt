stylesheet "1.0";
include "../tstp/tstp2wsx.xsl";
include "../utils/token-strings.xsl";
output method="xml" indent="yes";

//////////////////////////////////////////////////////////////////////
// Stylesheet parameters
//////////////////////////////////////////////////////////////////////

// A token list whose token are comma-separated
//
//   <step-name>:<path of XMLized E prover solution>
//
// The intention the step <step-name> will be proved using the
// contents contained in the document with the indicated path.
#repairs = { ""; }

//////////////////////////////////////////////////////////////////////
// Templates
//////////////////////////////////////////////////////////////////////

tpl [*] mode="copy" {
  $n = `name (.)`;
  <$n {
    for-each [@*] { copy-of `.`; }
    apply [*] mode="copy";
  }
}

tpl every-repair-proof-exists (#repairs) {
  $empty = token-string-is-empty (#token-string = $repairs);
  if [$empty = "1"] {
    "1";
  } else {
    $head = token-string-head (#token-string = $repairs);
    $tail = token-string-tail (#token-string = $repairs);
    $path = `substring-after ($head, ":")`;
    if [document ($path, /)] {
      every-repair-proof-exists (#repairs = $tail);
    } else {
      "0";
    }
  }
}

tpl [/] {
  if [tstp] {
    if [$repairs = ""] {
      apply [tstp] mode="copy";
    } else {
      $valid-token-string = is-valid-token-string (#string = $repairs);
      if [$valid-token-string = "1"] {
        $every-repair-proof-exists = every-repair-proof-exists (#repairs = $repairs);
        if [$every-repair-proof-exists = "1"] {
          apply [tstp];
        } else {
          msg terminate="yes" "Error: at least one of the given repair proof documents does not exist (or could not be loaded)";
        }
      } else {
        msg terminate="yes" "Error: the string given as the value of the repairs stylesheet parameter is not a valid token string.";
      }
    }

  } else {
    msg terminate="yes" "Error: the required tstp document element is missing.";
  }
}

tpl [tstp] {
  <Text-Proper {
    @articleid = "ARTICLE";
    <Item { @kind = "Section-Pragma"; }
    for-each [formula[@status = "axiom"]] {
      apply [.] mode="axiom";
    }
    for-each [formula[not(@status = "axiom") and source/non-logical-data[@name = "inference"]]] {
      $depends-on-conjecture = depends-on-conjecture ();
      if [$depends-on-conjecture = "0"] {
        apply [.] mode="lemma";
      }
    }
    for-each [formula[@status = "conjecture"]] {
      apply [.] mode="conjecture";
    }
  }
}

tpl [formula[@name]] {
  $n = `@name`;
  $pseudo-token = `concat (",", $n, ":")`;
  if [contains ($repairs, $pseudo-token)] {
    msg terminate="yes" "found you you fucker";
    $after-pseudo-token = `substring-after ($repairs, $pseudo-token)`;
    $proof-path = `substring-before ($after-pseudo-token, ",")`;
    <Item {
      @kind = "Regular-Statement";
      @shape = "Compact-Statement";
      <Proposition {
        <Label {
          @spelling = $n;
        }
        apply [*[1]];
      }
      <Block {
        @kind = "Proof";
        for-each [document ($proof-path, /)] {
          if [tstp] {
            apply [.] (#step = $n) mode="imported-eprover-proof";
          } else {
            $message = `concat ("Error: the XML document ", $proof-path, " is missing the needed tstp root element.")`;
            apply [.] (#message = $message) mode="die";
          }
        }
      }
    }
  } else {
    msg terminate="yes" "No way jose";
    apply [.] mode="copy";
  }
}

tpl [*] (#step) mode="imported-eprover-proof" {
  msg terminate="yes" "Error: unhandled node in imported-eprover-proof mode.";
}

tpl [*] (#step) mode="imported-eprover-axiom" {
  msg terminate="yes" "Error: unhandled node in imported-eprover-axiom mode.";
}

tpl [formula[@name]] (#step) mode="imported-eprover-axiom" {
  $n = `@name`;
  <Item {
    @kind = "Regular-Statement";
    @shape = "Compact-Statement";
    <Proposition {
      <Label {
        @spelling = `@name`;
      }
      apply [*[1]] mode="strip-universal-prefix";
    }
    <Straightforward-Justification {
      <Local-Reference {
        @spelling = $step;
      }
    }
  }
}

tpl [*] (#step) mode="imported-eprover-conclusion" {
  msg terminate="yes" "Error: unhandled node in imported-eprover-conclusion mode.";
}

tpl [*] (#step) mode="imported-eprover-lemma" {
  msg terminate="yes" "Error: unhandled node in imported-eprover-lemma mode.";
}

tpl [non-logical-data[@name = "inference"]] (#step) mode="imported-eprover-lemma" {
  for-each [non-logical-data[position() = last()]] {
    for-each [non-logical-data[@name]] {
      $n = `@name`;
      if [key ("splits", $n)] {

      } else {
        <Local-Reference {
          @spelling = `concat ($n, $step)`;
        }
      }
    }
  }
}

tpl [source] (#step) mode="imported-eprover-lemma" {
  for-each [non-logical-data[@name = "inference"]] {
    apply [.] (#step = $step) mode="imported-eprover-lemma";
    if [non-logical-data[@name = "skolemisation"]] {
      <Definition-Reference {
        @spelling = "SKOLEM";
        @"number" = `count (preceding::formula[source/non-logical-data[@name = "inference"]/non-logical-data[@name = "skolemisation"]]) + 1`;
      }
    }
  }
}

tpl [formula[@name]] (#step) mode="imported-eprover-lemma" {
  <Item {
    @kind = "Regular-Statement";
    @shape = "Compact-Statement";
    <Proposition {
      <Label {
        @spelling = `@name`;
      }
      apply [*[1]] mode="strip-universal-prefix";
    }
    <Straightforward-Justification {
      for-each [source] {
        apply [.] (#step = $step) mode="imported-eprover-lemma";
      }
    }
  }
}

tpl [tstp] (#step) mode="imported-eprover-proof" {
  for-each [formula[@status = "axiom"]] {
    apply [.] (#step = $step) mode="imported-eprover-axiom";
  }
  <Item {
    @kind = "Regular-Statement";
    @shape = "Diffuse-Statement";
    <Block {
      @kind = "Now-Reasoning";
      for-each [formula[not(@status = "axiom")]] {
        choose {
          when [@status = "negated_conjecture"] {
            <Item {
              @kind = "Assumption";
              <Single-Assumption {
                apply [.] mode="no-justification";
              }
            }
          }
          when [source/non-logical-data[@name = "introduced"]/non-logical-data[@name = "backtracking_splitting_component"]] {
            apply [.] mode="private-predicate-definition";
          }
          when [position() = last()] {
            apply [.] (#step = $step) mode="imported-eprover-conclusion";
          }
          otherwise {
            apply [.] (#step = $step) mode="imported-eprover-lemma";
          }
        }
      }
    }
  }
  <Item {
    @kind = "Conclusion";
    @shape = "Compact-Statement";
    <Thesis;
    <Straightforward-Justification {
      <Link;
    }
  }
}


//////////////////////////////////////////////////////////////////////
// Keys
//////////////////////////////////////////////////////////////////////

key "splits"
    [formula[@name and source/non-logical-data[@name = "introduced"]/non-logical-data[@name = "backtracking_splitting_component"]]]
    `@name`;

//////////////////////////////////////////////////////////////////////
// Templates
//////////////////////////////////////////////////////////////////////

tpl [tstp] mode="nested" {
  for-each [formula[@status = "axiom"]] {
    apply [.] mode="axiom";
  }
  for-each [formula[not(@status = "axiom") and source/non-logical-data[@name = "inference"]]] {
    $depends-on-conjecture = depends-on-conjecture ();
    if [$depends-on-conjecture = "0"] {
      apply [.] mode="lemma";
    }
  }
  for-each [formula[@status = "conjecture"]] {
    apply [.] mode="conjecture";
  }
}

tpl [*] mode="private-predicate-definition" {
  apply [.] (#message = "Error: we have reached an unhandled node in private-predicate-definition mode.") mode="die";
}

tpl [formula[source/non-logical-data[@name = "introduced"]/non-logical-data[@name = "backtracking_splitting_component"]]] mode="private-predicate-definition" {
  for-each [*[1]] {
    <Item {
      @kind = "Private-Predicate-Definition";
      for-each [*[position() = last()]] {
        choose {
          when [self::negation] {
            msg "negation case";
            if [defined-predicate[@name]] {
              for-each [defined-predicate[@name]] {
                <Variable {
                  @spelling = `@name`;
                }
                <Type-List;
                <Negated-Formula {
                  for-each [parent::negation] {
                    apply [preceding-sibling::*[1]];
                  }
                }
              }
            } else {
              $n = `name (.)`;
              $message = `concat ("Error: Don't know how to handle a ", $n, " node.  Where is the predicate definition?")`;
              apply [.] (#message = $message) mode="die";
            }
          }
          when [defined-predicate[@name]] {
            for-each [defined-predicate[@name]] {
              <Variable {
                @spelling = `@name`;
              }
              <Type-List;
              apply [preceding-sibling::*[1]];
            }
          }
          otherwise {
            $n = `name (.)`;
            $message = `concat ("Error: Don't know how to handle a ", $n, " node.  Where is the predicate definition?")`;
            apply [.] (#message = $message) mode="die";
          }
        }
      }
    }
  }
}

tpl [tstp] mode="flat" {
  for-each [formula[@status = "axiom"]] {
    apply [.] mode="axiom";
  }
  <Item {
    @kind = "Regular-Statement";
    @shape = "Diffuse-Statement";
    <Label {
      @spelling = "Refutation";
    }
    <Block {
      @kind = "Now-Reasoning";
      for-each [formula[not(@status = "axiom")]] {
        choose {
          when [@status = "negated_conjecture"] {
            <Item {
              @kind = "Assumption";
              <Single-Assumption {
                apply [.] mode="no-justification";
              }
            }
          }
          when [source/non-logical-data[@name = "introduced"]/non-logical-data[@name = "backtracking_splitting_component"]] {
            apply [.] mode="private-predicate-definition";
          }
          when [position() = last()] {
            apply [.] mode="conclusion";
          }
          otherwise {
            apply [.] mode="lemma";
          }
        }
      }
    }
  }
  <Item {
    @kind = "Theorem-Item";
    for-each [formula[@status = "negated_conjecture"]] {
      <Proposition {
        apply [*[1]] mode="unnegate"; // render the formula
      }
    }
    <Straightforward-Justification {
      <Local-Reference {
        @spelling = "Refutation";
      }
    }
  }
}

tpl [tstp[formula[@status = "negated_conjecture"]]] {
  $aid-uc = uc (#s = $article);
  <Text-Proper {
    @articleid = $aid-uc;
    <Item {
      @kind = "Section-Pragma";
    }
    reserve-variables ();
    choose {
      when [$shape = "flat"] {
        apply [.] mode="flat";
      }
      when [$shape = "nested"] {
        apply [.] mode="nested";
      }
      otherwise {
        apply [.] mode="flat";
      }
    }
  }
}

tpl [non-logical-data[@name = "inference"]] {
  for-each [non-logical-data[position() = last()]] {
    for-each [non-logical-data[@name]] {
      $n = `@name`;
      if [key ("splits", $n)] {

      } else {
        <Local-Reference {
          @spelling = `@name`;
        }
      }
    }
  }
}

tpl [source] {
  for-each [non-logical-data[@name = "inference"]] {
    apply [.];
    if [non-logical-data[@name = "skolemisation"]] {
      <Definition-Reference {
        @spelling = "SKOLEM";
        @"number" = `count (preceding::formula[source/non-logical-data[@name = "inference"]/non-logical-data[@name = "skolemisation"]]) + 1`;
      }
    }
  }
}

tpl [formula[@name]] mode="lemma" {
  $n = `@name`;
  $pseudo-token = `concat (",", $n, ":")`;
  <Item {
    @kind = "Regular-Statement";
    @shape = "Compact-Statement";
    <Proposition {
      <Label {
        @spelling = `@name`;
      }
      apply [*[1]] mode="strip-universal-prefix";
    }
    if [contains ($repairs, $pseudo-token)] {
      $after-pseudo-token = `substring-after ($repairs, $pseudo-token)`;
      $proof-path = `substring-before ($after-pseudo-token, ",")`;
      <Block {
        @kind = "Proof";
        for-each [document ($proof-path, /)] {
          if [tstp] {
            apply [.] (#step = $n) mode="imported-eprover-proof";
          } else {
            $message = `concat ("Error: the XML document ", $proof-path, " is missing the needed tstp root element.")`;
            apply [.] (#message = $message) mode="die";
          }
        }
      }
    } else {
      <Straightforward-Justification {
        for-each [source] {
          apply [.];
        }
      }
    }
  }
}

tpl [defined-predicate[@name and not(@name = "true") and not(@name = "false")]] {
  <Private-Predicate-Formula {
    @spelling = `@name`;
  }
}

tpl [*] mode="unnegate" {
  apply [.] (#message = "Error: we have reached an unhandled node in unnegate mode.") mode="die";
}

tpl [negation] mode="unnegate" {
  apply [*[1]];
}

tpl [formula[@name and not(@name = "")]] mode="conclusion" {
  <Item {
    @kind = "Conclusion";
    @shape = "Compact-Statement";
    <Proposition {
      apply [*[1]];
    }
    <Straightforward-Justification {
      for-each [source] {
        apply [.];
      }
    }
  }
}